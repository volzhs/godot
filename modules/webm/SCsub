#!/usr/bin/env python

Import('env')
Import('env_modules')

env_webm = env_modules.Clone()

# Thirdparty source files
thirdparty_libsimplewebm_dir = "#thirdparty/libsimplewebm/"
thirdparty_libsimplewebm_sources = [
	"libwebm/mkvparser/mkvparser.cc",
	"OpusVorbisDecoder.cpp",
	"VPXDecoder.cpp",
	"WebMDemuxer.cpp",
]
thirdparty_libsimplewebm_sources = [thirdparty_libsimplewebm_dir + file for file in thirdparty_libsimplewebm_sources]

env_webm.add_source_files(env.modules_sources, thirdparty_libsimplewebm_sources)
env_webm.Append(CPPPATH = [thirdparty_libsimplewebm_dir, thirdparty_libsimplewebm_dir + "libwebm/"])

# also requires libogg, libvorbis and libopus
if (env["libogg"] != "system"): # builtin
	env_webm.Append(CPPPATH = ["#thirdparty/libogg"])
if (env["libvorbis"] != "system"): # builtin
	env_webm.Append(CPPPATH = ["#thirdparty/libvorbis"])
if (env["opus"] != "system"): # builtin
	env_webm.Append(CPPPATH = ["#thirdparty"])

if (env["libvpx"] != "system"): # builtin
	Export('env_webm')
	SConscript("libvpx/SCsub")

# Godot source files
env_webm.add_source_files(env.modules_sources, "*.cpp")
