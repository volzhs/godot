#!/usr/bin/env python

Import('env')
Import('env_modules')

env_vhacd = env_modules.Clone()

# Thirdparty source files

thirdparty_dir = "#thirdparty/vhacd/"

thirdparty_sources = [
"src/vhacdManifoldMesh.cpp",
"src/FloatMath.cpp",
"src/vhacdMesh.cpp",
"src/vhacdICHull.cpp",
"src/vhacdVolume.cpp",
"src/VHACD-ASYNC.cpp",
"src/btAlignedAllocator.cpp",
"src/vhacdRaycastMesh.cpp",
"src/VHACD.cpp",
"src/btConvexHullComputer.cpp"
]

thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_vhacd.Append(CPPPATH=[thirdparty_dir+"/inc"])
env_vhacd.Append(CPPFLAGS=["-DGODOT_ENET"])

# upstream uses c++11
if not env.msvc:
	env_vhacd.Append(CCFLAGS="-std=c++11")

env_thirdparty = env_vhacd.Clone()
env_thirdparty.disable_warnings()
env_thirdparty.add_source_files(env.modules_sources, thirdparty_sources)

env_vhacd.add_source_files(env.modules_sources, "*.cpp")
